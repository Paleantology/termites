#####################################
# GTR+G nucleotide substitution model #
#####################################

for (i in 1:n_data_subsets) {

    # exchangeability rates for partition i
    er_prior[i] <- v(1,1,1,1,1,1)
    er[i] ~ dnDirichlet(er_prior[i])
    moves.append(mvSimplexElementScale(er[i], alpha=10, tune=true, weight=3))

    # stationary frequencies for partition i
    pi_prior[i] <- v(1,1,1,1)
    pi[i] ~ dnDirichlet(pi_prior[i])
    moves.append(mvSimplexElementScale(pi[i], alpha=10, tune=true, weight=2) )

    # rate matrix for partition i
    Q[i] := fnGTR(er[i],pi[i])

    # +Gamma for partition i
    alpha_prior_mean[i] <- 5.0
    alpha_prior_sd[i] <- 0.587405
    alpha[i] ~ dnLognormal( alpha_prior_mean[i], alpha_prior_sd[i] )
    gamma_rates[i] := fnDiscretizeGamma( alpha[i], alpha[i], 4, false )

    # add moves for the alpha parameter
    moves.append(mvScale(alpha[i],weight=2))

    # the probability of a site being invariable
    pinvar[i] ~ dnBeta(1,1)
    moves.append(mvScale(pinvar[i], lambda=0.1, tune=true, weight=2.0))
    moves.append(mvSlide(pinvar[i], delta=0.1, tune=true, weight=2.0))

}

###################
# PhyloCTMC Model #
###################

# the sequence evolution model
for (i in 1:n_data_subsets) {
    phyloSeq[i] ~ dnPhyloCTMC(tree=fbd_tree, Q=Q[i], siteRates=gamma_rates[i], branchRates = branch_rates, pInv=pinvar[i], type="DNA")
    phyloSeq[i].clamp(data[i])
}
